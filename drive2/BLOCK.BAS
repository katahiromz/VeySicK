100 ' BLOCK {8801} {GRPH} {DELAY2:10}
110 '--------------------------------------------------------------------------
120 *INIT
130 SCREEN 0,0:CONSOLE 0,25,0,1:COLOR 7,0:WIDTH 40,25
140 NEW CMD
150 VRAM=&HF3C8:W=20
160 REMAINS=3
170 '--------------------------------------------------------------------------
180 *RETRY
190 CLS 3
200 BX=1:BY=23:DX=1:DY=-1:RLEN=5:RX=1+(W-RLEN)/2:RY=24
210 *DRAWTABLE
220 COLOR 6:LOCATE 0,0:PRINT "‰"+STRING$(W, "Ü")+"Â";
230 FOR Y=1 TO 23:LOCATE 0,Y:PRINT "á";SPACE$(W);"á";:NEXT Y
240 LOCATE 0,24:PRINT "Á"+SPACE$(W)+"Ê";
250 CNT1=0
260 FOR Y=4 TO 8:FOR X=1 TO W-1 STEP 2
270 COLOR 2:LOCATE X,Y:PRINT "€€";
280 CNT1=CNT1+1
290 NEXT X,Y
300 CNT2=0:SCORE=0
310 COLOR 7:LOCATE 1+W+2,0:PRINT "Ãﬁ€Ø∏ ∏Ωﬁº";
320 COLOR 5:LOCATE 1+W+2,1:PRINT STRING$(REMAINS,"=")+SPACE$(3-REMAINS);
330 COLOR 7:LOCATE 1+W+3,8:PRINT "‘ºﬁŸº ∑∞ √ﬁ";
340 COLOR 7:LOCATE 1+W+3,9:PRINT "ø≥ª º√» !!";
350 '--------------------------------------------------------------------------
360 *STARTWAIT
370 BASE$="T120@0O3Q7L8BB-A-FA-4AF"
380 GUITAR$="T120@0O4Q7L16BBB-B-A-A-A-FE-E-F8A-A-F8"
390 CMD PLAY BASE$,GUITAR$
400 COLOR 3:LOCATE 1+(W-LEN("PRESS SPACE"))/2,12:PRINT "PRESS SPACE";
410 WHILE INKEY$<>"":WEND
420 WHILE INKEY$<>" ":R=RND(1):WEND
430 BX=1+INT(RND(1)*W)
440 IF (RND(1)*3) MOD 2 THEN DX=-1 ELSE DX=1
450 COLOR 6:LOCATE 1+(W-LEN("PRESS SPACE"))/2,12:PRINT SPACE$(11);
460 '--------------------------------------------------------------------------
470 *MAINLOOP
480 BEEP 0
490 FLAG=0
500 IF BY=24 THEN REMAINS=REMAINS-1:FLAG=1
510 IF FLAG THEN IF REMAINS=0 THEN *GAMEOVER ELSE GOTO *RETRY
520 MX=0
530 IF RX>1 AND (INP(&H0A) AND 4)=0 THEN MX=-1
540 IF (RX+RLEN<W) AND (INP(&H08) AND 4)=0 THEN MX=1
550 IF MX<>0 THEN LOCATE RX,RY:PRINT SPACE$(RLEN);
560 RX=RX+MX
570 COLOR 5:LOCATE RX,RY:PRINT STRING$(RLEN,"=");
580 X=BX+DX:Y=BY+DY
590 C$=CHR$(PEEK(VRAM+(80+40)*Y+X*2))
600 IF C$="‰" OR C$="Â" OR C$="Á" OR C$="Ê" THEN DX=-DX:DY=-DY:GOTO *MAINLOOP
610 IF C$="á" THEN DX=-DX:GOTO *MAINLOOP
620 IF C$="Ü" OR C$="=" THEN DY=-DY:GOTO *MAINLOOP
630 IF C$="€" THEN GOSUB *HITBLOCK:GOTO *MAINLOOP
640 IF CNT1<=CNT2 GOTO *GAMECLEAR
650 LOCATE BX,BY:PRINT " ";
660 BX=BX+DX:BY=BY+DY
670 COLOR 7:LOCATE BX,BY:PRINT "Ï";
680 FOR DELAY=0 TO 100:NEXT DELAY
690 GOTO *MAINLOOP
700 '--------------------------------------------------------------------------
710 *HITBLOCK
720 BEEP 1
730 DY=-DY:CNT2=CNT2+1
740 LOCATE INT((X-1)/2)*2+1,Y:PRINT "  ";
750 SCORE=SCORE+10
760 COLOR 7:LOCATE 1+W+2,2:PRINT USING "SCORE: #####";SCORE;
770 FOR I=0 TO 10:NEXT I
780 RETURN
790 '--------------------------------------------------------------------------
800 *GAMEOVER
810 CMD PLAY "T120@2L8V15BAEDC2"
820 COLOR 5:LOCATE 1+W+2,1:PRINT STRING$(REMAINS,"=")+SPACE$(3-REMAINS);
830 COLOR 3:LOCATE 1+(W-LEN("GAME OVER"))/2,12:PRINT "GAME OVER";:LOCATE 0,24
840 END
850 '--------------------------------------------------------------------------
860 *GAMECLEAR
870 BEEP 0
880 CMD PLAY "T120@49L8V15CCDDEEFFERDRCR"
890 COLOR 6:LOCATE 1+(W-LEN("CLEAR!"))/2,12:PRINT "CLEAR!";:LOCATE 0,24
900 END
910 '--------------------------------------------------------------------------
