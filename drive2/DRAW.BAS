100 '***** DRAW.BAS / 1994-10-06 by J.Abe *****
110 SCREEN 3:CONSOLE 0,25,0,1:CLS 3:COLOR ,,,,2:WIDTH 80,25:COLOR 6:LOCATE 0,6
120 PRINT,,"* * * * * * * * * * * * * * *"
130 PRINT,,"*                           *"
140 PRINT,,"*   PROGRAM  Ｄ Ｒ Ａ Ｗ    *"
150 PRINT,,"*                           *"
160 PRINT,,"*     (マウスで絵を描く)    *"
170 PRINT,,"*                           *"
180 PRINT,,"* * * * * * * * * * * * * * *"
190 COLOR 7:LOCATE 18,18:INPUT "線タイプは曲線と直線に切り替わります (OK:ﾘﾀｰﾝ) ",T$
200 '***** JUNBI ********
210 TYP=1:FLAG=0:MOUSE 0
220 COLOR=(1,&H999):COLOR=(2,&HFFF):COLOR=(15,&HDDD)
230 COLOR=(3,&HFF0):COLOR=(4,&HEB0):COLOR=(5,&HF00):COLOR=(6,&HC34)
240 COLOR=(7,&HF0F):COLOR=(8,&HA0D):COLOR=(9,&HF):COLOR=(10,&HAE)
250 COLOR=(11,&HEA):COLOR=(12,&HF0):COLOR=(13,&H6A0):COLOR=(14,&HBE8)
260 CLS:LINE(80,40)-(638,398),15,B:LINE(79,39)-(639,399),15,B
270 FOR F=0 TO 14:LINE(10,F*17+144)-(70,F*17+161),15,BF,F:NEXT F
280 LINE(4, 44)-(75, 67),6,B:LOCATE 1,3:PRINT "線タイプ "
290 LINE(4, 76)-(75, 99),6,B:LOCATE 1,5:PRINT "色を塗る"
300 LINE(4,108)-(75,131),6,B:LOCATE 1,7:PRINT "終 わ り "
310 COLOR 6:LOCATE 6,0:PRINT "右ボタンを押すとモードが変わって左上の操作を選べます"
320 COLOR 7:LOCATE 64,0:PRINT " [カーソル位置] ":COLOR 5
330 MODE=0:MOUSE 4,80,40,638,398:MOUSE 1,,,1:MOUSE(2) ON:MOUSE(3) ON
340 '****************************************
350 *TYP:ON TYP GOTO 370,430
360 '***** KYOKUSEN *****
370 LOCATE 6,1:PRINT "《曲線》左ボタンを押して動かすと線が引けます"
380 ON MOUSE(2) GOSUB *KYOK
390 ON MOUSE(3) GOSUB *MODE
400 ON MOUSE(4) GOSUB *DEND:MOUSE(4) ON
410 GOTO *MOVE
420 '***** CHOKUSEN *****
430 LOCATE 6,1:PRINT "《直線》左ボタンを押すと線が引けます　　　　"
440 ON MOUSE(2) GOSUB *CHOK
450 ON MOUSE(3) GOSUB *MODE
460  ' ***** M O V E  *****
470 *MOVE
480 MOUSE(2) STOP:MOUSE(3) STOP
490 LOCATE 65,1:PRINT USING "X=#### Y=####";MOUSE(0),MOUSE(1)
500 MOUSE(2) ON:MOUSE(3) ON
510 GOTO *MOVE
520 '********** SUBROUTINE ******************
530 *KYOK
540 IF MODE>0 THEN 680
550 FLAG=1:POINT(MOUSE(0),MOUSE(1))
560 MOUSE 1,,,0:LINE -(MOUSE(0),MOUSE(1)),15:MOUSE 1,,,1
570 IF FLAG=1 THEN 560 ELSE RETURN *MOVE
580 *DEND
590 FLAG=0:RETURN
600 '********************
610 *CHOK
620 IF MODE>0 THEN 680
630 IF FLAG=0 THEN FLAG=1:POINT(MOUSE(0),MOUSE(1)):LOCATE 24,1:PRINT "２回押すと引くのをやめます"
640 IF (X1-MOUSE(4,1))^2+(Y1-MOUSE(5,1))^2<3 THEN 660
650 X1=MOUSE(4,1):Y1=MOUSE(5,1):MOUSE 1,,,0:LINE -(X1,Y1),15:MOUSE 1,,,1:RETURN
660 FLAG=0:LOCATE 24,1:PRINT "押すと線が引けます　　　　":RETURN
670 '********************
680 IF MODE=2 THEN 740
690 IF MOUSE(1)>105 THEN RETURN *PEND
700 IF MOUSE(1)>70 THEN MODE=2:MOUSE 4,0,145,80,398:MOUSE 1,50,280:RETURN
710 IF TYP=1 THEN TYP=2:MOUSE(4) STOP ELSE TYP=1
720 MODE=0:MOUSE 4,80,40,638,398:MOUSE 1,320,200:GOTO *TYP
730 '********************
740 F=INT((MOUSE(1)-145)/17)
750 PAINT(MOUSE(4,2),MOUSE(5,2)),F,15
760 MODE=0:MOUSE 4,80,40,638,398:MOUSE 1,MOUSE(4,2),MOUSE(5,2):RETURN
770 '********************
780 *MODE
790 IF MODE>0 THEN 820
800 FLAG=0:IF TYP=2 THEN LOCATE 24,1:PRINT "押すと線が引けます　　　　"
810 MODE=1:MOUSE 4,0,42,80,140:MOUSE 1,50,90:RETURN
820 MODE=0:MOUSE 4,80,40,638,398:MOUSE 1,350:RETURN
830 '****************************************
840 *PEND:MOUSE 6:COLOR 7:CLS 3:SCREEN 0:CONSOLE 0,25,1,1:END
850 '****************************** END *****
