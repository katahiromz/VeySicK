100 GOSUB *INIT
110 GOSUB *HM
120 COL=3
130 GOSUB *PD
140 L=100:GOSUB *SIKAKU
200 END
1000 *FD:'前進　　進む量は変数Ｌに入れて呼び出す。色変数ＣＯＬ
1010 X0=X:Y0=Y:X=X0+EX*L:Y=Y0+EY*L
1020 IF PN=1 THEN LINE(X0,Y0)-(X,Y),COL
1030 RETURN
1100 *RT:'右へ方向転換　　回転角は変数ＫＡＫＵに入れて呼び出す。
1110 THETA=THETA+2*PI/360*KAKU
1120 EX=COS(THETA):EY=SIN(THETA)
1130 RETURN
1200 *LT:'左へ方向転換　　回転角は変数ＫＡＫＵに入れて呼び出す。
1210 THETA=THETA-2*PI/360*KAKU
1220 EX=COS(THETA):EY=SIN(THETA)
1230 RETURN
1300 *PD:'ペン・ダウン（描画開始）
1310 PN=1:RETURN
1400 *PU:'ペン・アップ（描画中断）
1410 PN=0:RETURN
1500 *HM:　'ホームポジションに戻る。（画面の中央で下向きに設定）
1510 X=320:Y=200:EX=0:EY=1:THETA=2*PI/360*90:RETURN
1600 *INIT:'初期化　　最初に実行すること。
1610 SCREEN 3:CLS 3:PI=3.1416:RETURN
1700 *CC:'ｃｈａｎｇｅ　ｃｏｌｏｒ　色を変える。
1710 COL=COL+1:IF COL>7 THEN COL=1
1720 RETURN
2000 *SIKAKU
2010 FOR I=1 TO 4
2020 GOSUB *FD
2030 KAKU=90:GOSUB *RT:GOSUB *CC
2040 NEXT I
2050 RETURN
