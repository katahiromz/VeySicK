10 WIDTH 80,25:CONSOLE 0,25,0,1:CLS 3:RANDOMIZE VAL(RIGHT$(TIME$,2))
20 DIM HOLE(2,2),NM$(10),SC(10),MS(10)
30 LV=1
40 '------------------------------------------タイトル画面
50 LOCATE 32, 8:PRINT "−　モグラ叩き　−"
60 LOCATE 27,12:PRINT "スペース／ゲームスタート"
70 LOCATE 27,14:PRINT "　　　Ｑ／ランキング初期化"
80 I$=INKEY$
90 IF I$="Q" OR I$="q" THEN *NEWGAME
100 IF I$<>" " THEN 80
110 CLS
120 '------------------------------------------入力
130 I$=INKEY$ : A=0 : B=0
140 IF I$="1" THEN A=0:B=2:GOSUB *CHECK
150 IF I$="2" THEN A=1:B=2:GOSUB *CHECK
160 IF I$="3" THEN A=2:B=2:GOSUB *CHECK
170 IF I$="4" THEN A=0:B=1:GOSUB *CHECK
180 IF I$="5" THEN A=1:B=1:GOSUB *CHECK
190 IF I$="6" THEN A=2:B=1:GOSUB *CHECK
200 IF I$="7" THEN A=0:B=0:GOSUB *CHECK
210 IF I$="8" THEN A=1:B=0:GOSUB *CHECK
220 IF I$="9" THEN A=2:B=0:GOSUB *CHECK
230 '------------------------------------------モグラの動き
240 FOR J=0 TO 2
250   FOR I=0 TO 2
260     IF HOLE(I,J)<>0 THEN HOLE(I,J)=HOLE(I,J)+1
270     IF HOLE(I,J)=0 AND INT(RND(1)*35)=0 THEN HOLE(I,J)=1
280     IF HOLE(I,J)>14-LV THEN HOLE(I,J)=0:MISS=MISS+1:IF MISS>10 THEN MISS=10
290   NEXT I
300 NEXT J
310 '------------------------------------------画面表示
320 FOR J=0 TO 2
330   FOR I=0 TO 2
340     IF HOLE(I,J)=0 THEN GOSUB *HOLE
350     IF HOLE(I,J)>0 THEN GOSUB *MOGURA
360     IF HOLE(I,J)<0 THEN GOSUB *HIT
370   NEXT I
380 NEXT J
390 LOCATE 60,5:PRINT "LEVEL":LOCATE 60,6:PRINT LV
400 LOCATE 60,9:PRINT "SCORE":LOCATE 60,10:PRINT SCORE
410 LOCATE 60,13:PRINT "MISS":LOCATE 60,14:PRINT MISS
420 '--------------------------------------------------------
430 IF MISS>=10 THEN COLOR 4:LOCATE 22,10:PRINT "GAME OVER" : GOTO *RANKING
440 IF LV>10 THEN COLOR 4:LOCATE 21,10:PRINT "GAME CLEAR!!" : GOTO *RANKING
445 FOR I=0 TO 200:NEXT I ' ウェイト
450 GOTO 130 'Return to main-loop.
460 '------------------------------------------モグラ表示
470 *MOGURA
480 LOCATE I*17+3,J*7  :PRINT "　┌───┐　"
490 LOCATE I*17+3,J*7+1:PRINT "　│○　○│　"
500 LOCATE I*17+3,J*7+2:PRINT "　│　　　│　"
510 LOCATE I*17+3,J*7+3:PRINT "　│＞○＜│　"
520 LOCATE I*17+3,J*7+4:PRINT "　│　　　│　"
530 LOCATE I*17+3,J*7+5:PRINT "─┴───┴─"
540 LOCATE I*17+8,J*7+6:PRINT (I+1)+(2-J)*3
550 RETURN
560 '
570 *HOLE
580 LOCATE I*17+3,J*7  :PRINT "　　　　　　　"
590 LOCATE I*17+3,J*7+1:PRINT "　　　　　　　"
600 LOCATE I*17+3,J*7+2:PRINT "　　　　　　　"
610 LOCATE I*17+3,J*7+3:PRINT "　　　　　　　"
620 LOCATE I*17+3,J*7+4:PRINT "　　　　　　　"
630 LOCATE I*17+3,J*7+5:PRINT "───────"
640 LOCATE I*17+8,J*7+6:PRINT (I+1)+(2-J)*3
650 RETURN
660 '
670 *HIT
680 LOCATE I*17+3,J*7  :PRINT "　　　　　　　"
690 LOCATE I*17+3,J*7+1:PRINT "　┌───┐　"
700 LOCATE I*17+3,J*7+2:PRINT "　│×　×│　"
710 LOCATE I*17+3,J*7+3:PRINT "　│　　　│　"
720 LOCATE I*17+3,J*7+4:PRINT "　│＞○＜│　"
730 LOCATE I*17+3,J*7+5:PRINT "─┴───┴─"
740 LOCATE I*17+8,J*7+6:PRINT (I+1)+(2-J)*3
750 RETURN
760 '---------------------------------------------合否判定
770 *CHECK
780 IF HOLE(A,B)=0 THEN MISS=MISS+1
790 IF HOLE(A,B)>0 THEN HOLE(A,B)=-2:SCORE=SCORE+1:COUNT=COUNT+1
800 IF COUNT=10 THEN COUNT=0:LV=LV+1
810 RETURN
820 '---------------------------------------------ランキング初期化
830 *NEWGAME
840 OPEN "RANKING.TXT" FOR OUTPUT AS #1
850 PRINT #1,"キング,100,3"
860 PRINT #1,"クイーン,100,5"
870 PRINT #1,"ジャック,100,7"
880 PRINT #1,"エース,100,9"
890 PRINT #1,"ハート,90,10"
900 PRINT #1,"ダイヤ,80,10"
910 PRINT #1,"スペード,70,10"
920 PRINT #1,"クラブ,60,10"
930 PRINT #1,"ジョーカー,50,10"
940 PRINT #1,"B.H.(--;,40,10"
950 CLOSE #1
960 CLS
970 LOCATE 20,11:PRINT "初期化しました。もう一度実行してください。"
980 END
990 '----------------------------------------------ランキング
1000 *RANKING
1010 LOCATE 13,16:PRINT "スペースキーを押してください":COLOR 7
1020 WHILE INKEY$<>" ":WEND
1030 '
1040 OPEN "RANKING.TXT" FOR INPUT AS #1
1050   FOR I=0 TO 9
1060     INPUT #1,NM$(I),SC(I),MS(I)
1070   NEXT I
1080 CLOSE #1
1090 '
1100 FOR I=0 TO 9  '--------ランクインしたかどうか先に調べる
1110   IF SC(I)*100+10-MS(I) <= SCORE*100+10-MISS THEN SW=1
1120 NEXT I
1130 IF SW=1 THEN GOSUB *RANKIN
1140 '
1150 CLS
1160 PRINT "SCORE RANKING TOP 10"
1170 PRINT
1180 PRINT "   RECORD FORMAT = RANK. NAME/SCORE/MISS"
1190 PRINT
1200 FOR I=0 TO 9
1210   PRINT I+1;". ";NM$(I);"/";SC(I);"/";MS(I)
1220 NEXT I
1230 '
1240 OPEN "RANKING.TXT" FOR OUTPUT AS #1
1250   FOR I=0 TO 9
1260     PRINT #1,NM$(I);",";SC(I);",";MS(I)
1270   NEXT I
1280 CLOSE #1
1290 END
1300 '---------------------------------------------ランクイン
1310 *RANKIN
1320 CLS
1330 PRINT "ランクインしました！"
1340 INPUT "名前を入力してください";NM$(10)
1350 SC(10)=SCORE
1360 MS(10)=MISS
1370 FOR X=0 TO 9
1380   FOR Y=X+1 TO 10
1390     S1=SC(X)*100+10-MS(X)
1400     S2=SC(Y)*100+10-MS(Y)
1410     IF S1<=S2 THEN SWAP NM$(X),NM$(Y):SWAP SC(X),SC(Y):SWAP MS(X),MS(Y)
1420   NEXT Y
1430 NEXT X
1440 RETURN
