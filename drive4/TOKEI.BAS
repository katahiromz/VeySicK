10 '  save "TOKEI.BAS",A
20 CONSOLE 0,25,0,1:SCREEN 3:COLOR 0:WIDTH 80,20
30  DIM XY(60,1)
40 CLS 3
50 GOSUB *TOKEI.SET
60 GOSUB *TOKEI:GOTO 60
70 '-------------時計---------
80 *TOKEI.SET
90 FOR I= -30 TO  30
100 XY(30-I,0)=COS(3.1416*(I/30 )):XY(30-I,1)=SQR(1-(XY(30-I,0)^2))*SGN(I)
110 NEXT I
120 '--
130 R=30:XX=100:YY=100 ''''''''R=サイズ:XX=位置(X):YY=位置(Y)'''''''''''
140 X0=R*1.5+XX:Y0= R*1.5+YY
150 R1=R*1.13:R2=R*1.15
160 LINE(X0-R*1.5 ,Y0-R*1.5  )-(X0+R*1.5  ,Y0+R*1.5  ),7,BF
170 LINE(X0-R*1.4 ,Y0-R*1.4  )-(X0+R*1.4  ,Y0+R*1.4  ),0,B
180 FOR I=0 TO 60
190 :LINE (X0+R1*XY(I,1),Y0+R1*XY(I,0))-(X0+R2* XY(I,1),Y0+R2*XY(I,0)),0
200 :NEXT I
210 R3=R:R4=R*1.3
220 FOR I=0 TO 60 STEP 5
230 :LINE (X0+R3*XY(I,1),Y0+R3*XY(I,0))-(X0+R4* XY(I,1),Y0+R4*XY(I,0)),0
240 :NEXT I
250 '--
260 S2=R*.9:M2=R*.8:H2=R*.6:S3=R*.2
270 GOSUB *HMS
280 DH.=H.-1:DM.=M.-1:DS.=S.-1
290 IF DH.<0 THEN DH.=59
300 IF DM.<0 THEN DM.=59
310 IF DS.<0 THEN DS.=59
320 GOSUB *H:GOSUB *M:GOSUB *S
330 RETURN
340 '====================
350 *TOKEI
360 GOSUB *HMS:IF DS.=S. THEN RETURN
370 IF DH.<>H. THEN GOSUB *H
380 IF DM.<>M. THEN GOSUB *M
390 GOSUB *S:RETURN
400 '=====================
410 *HMS
420 S.=VAL(MID$(TIME$,7,2))
430 M.=VAL(MID$(TIME$,4,2))
440 H.=((VAL(MID$(TIME$,1,2)) MOD 12)*5)+M.\12
450 RETURN
460 '--
470 *S
480 DSS=DS.-30:IF DSS<0 THEN DSS=DS.+30
490  SS= S.-30:IF  SS<0 THEN  SS= S.+30
500 LINE(X0+S2*XY(DS.,1),Y0+S2*XY(DS.,0))-(X0+S3* XY(DSS,1),Y0+S3*XY(DSS,0)),7
510 LINE(X0+S2* XY(S.,1),Y0+S2*XY(S.,0))-(X0+S3* XY(SS,1),Y0+S3*XY(SS,0)),0
520 LINE (X0,Y0)-(X0+M2* XY(M.,1),Y0+M2*XY(M.,0)),0
530 LINE (X0,Y0)-(X0+H2* XY(H.,1),Y0+H2*XY(H.,0)),0
540 DS.=S.
550 RETURN
560 '--
570 *M:LINE (X0,Y0)-(X0+M2* XY(DM.,1),Y0+M2*XY(DM.,0)),7:DM.=M.:RETURN
580 '--
590 *H:LINE (X0,Y0)-(X0+M2* XY(DH.,1),Y0+M2*XY(DH.,0)),7:DH.=H.:RETURN
600 '-
